usePlugin 'groovy'
usePlugin 'eclipse'

archivesBaseName = 'groovy-aop'
version = '0.6.1'

repositories {
    mavenCentral()
    mavenRepo urls: ['http://repository.codehaus.org/', 'http://download.java.net/maven/2/']
}
test {
    scanForTestClasses = false
    excludes = ["**/*Support.class", "**/UnitTestAsScriptTest.class"]
    options.fork( jvmArgs: [ "-javaagent:$libsDir/${jar.archiveName}"] )
}
configurations {
    textile
}

dependencies {
    groovy		files('lib/groovy-all-1.6.4.jar')
    testCompile group: 'junit',               name: 'junit',          version: '3.8.2'
    textile     group: 'net.java',            name: 'textile-j',      version: '2.2'
    compile(
               [group: 'org.aspectj',         name: 'aspectjweaver',  version: '1.6.5'],
               [group: 'org.antlr',           name: 'antlr-runtime',  version: '3.1.3'],
               [group: 'org.antlr',           name: 'stringtemplate', version: '3.2'],
               [group: 'asm',                 name: 'asm-all',        version: '3.1']
    )
}

task generateDoc << {
    ant.taskdef(
        resource:  'net/java/textilej/util/anttask/tasks.properties',
        classpath: configurations.textile.asPath
    )
    ant.'textile-to-html' {
        fileset(dir: 'userguide') {
            include(name: '*.textile')
        }
    }
}

task gen_aacs << {
    def metaClassPackage = 'src/main/java/org/codehaus/groovy/aop/metaclass'
    ['gen_aacs':    'AspectAwareCallSite',
     'gen_adv_inv': 'AdviceInvoker'].each { k, v ->
        ant.exec( dir:'.', executable:'bash', output:"${metaClassPackage}/${v}.java") {
            arg line:"-c \"\$GROOVY_HOME/bin/groovy ${metaClassPackage}/${k}.groovy\""
        }
    }
}

task install(dependsOn: libs) << {
    ant.copy( file:"$libsDir/${jar.archiveName}", todir:"C:/groovy/groovy-1.6.4/lib" )
}

manifest.mainAttributes(
    "Premain-Class":           "org.codehaus.groovy.gjit.agent.Agent",
    "Can-Redefine-Classes":    "true",
    "Can-Retransform-Classes": "true"
)